# ==============================================================================
# TAPIRS
# A reproducible metabarcoding workflow using snakemake
# ==============================================================================

configfile: "config/config.yaml"

# ------------------------------------------------------------------------------
# LIBRARY AND SAMPLE INFORMATION
# ------------------------------------------------------------------------------

import pandas as pd

libraries_df = pd.read_table(config['samples'], header = None)  # Pull in libraries and samples from tsv

# Generate library wildcards
LIBRARIES = list(libraries_df[0])  # create list from column of dataframe
LIBRARIES = list(dict.fromkeys(LIBRARIES))  # dipping it into a dictionary to remove duplicates

# Generate samples wildcards
SAMPLES = list(libraries_df[1])  # create list from column of dataframe
SAMPLES = list(dict.fromkeys(SAMPLES))  # dipping into a dictionary to remove duplicates

# Generate list of legitimate combinations
with open(config['samples']) as infile:
    real_combos = []
    for line in infile:
        real_combos.append(line.strip().replace('\t', '/'))

# ------------------------------------------------------------------------------
# CONDITIONAL RULE ALL
# ------------------------------------------------------------------------------

myoutput = list()

blast = "results/" + config['my_experiment'] + "_blast" + str(config['MLCA_identity']) + "_" + config['cluster_method'] + ".tsv",
kraken2 = "results/" + config['my_experiment'] + "_kraken2_conf" + str(config['kraken2_confidence']).split('.')[1] + "_" + config['cluster_method'] + ".tsv"

if config["analysis_method"] == "blast" or config["analysis_method"] == "both":
    myoutput.append(blast)

if config["analysis_method"] == "kraken2" or config["analysis_method"] == "both":
    myoutput.append(kraken2)

rule all:
    input:
        myoutput

# ------------------------------------------------------------------------------
# INCLUCE CONDITIONAL RULE FILES
# ------------------------------------------------------------------------------

include: "rules/qc.smk"
include: "rules/lca.smk"
include: "rules/vsearch.smk"

if config["analysis_method"] == "blast" or config["analysis_method"] == "both":
    include: "rules/blast.smk"

if config["analysis_method"] == "kraken2" or config["analysis_method"] == "both":
    include: "rules/kraken2.smk"
